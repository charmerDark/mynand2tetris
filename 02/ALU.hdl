// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
	And16(a=x,b=false,out=zrx);
	Mux16(a=x,b=zrx,sel=zx,out=x1);
	Not16(in=x1,out=notx1);
	Mux16(a=x1,b=notx1,sel=nx,out=prex);
	And16(a=y,b=false,out=zry);
	Mux16(a=y,b=zry,sel=zy,out=y1);
	Not16(in=y1,out=noty1);
	Mux16(a=y1,b=noty1,sel=ny,out=prey);
	Add16(a=prex,b=prey,out=xpy);
	And16(a=prex,b=prey,out=xay);
	Mux16(a=xay,b=xpy,sel=f,out=pout);
	Not16(in=pout,out=npout);
	Mux16(a=pout,b=npout,sel=no,out=out,out[15]=ck1,out[0..7]=t1,out[8..15]=t2);
	And(a=ck1,b=true,out=ng);
	Or8Way(in=t1,out=check1);
	Or8Way(in=t2,out=check2);
	Or(a=check1,b=check2,out=nzr);
	Not(in=nzr,out=zr);
	}